{-# OPTIONS --without-K #-}

open import reedy.IndexSemicategories

module reedy.LinearSieves2 {ℓₘ} (I : SuitableSemicategory ℓₘ) where

open SuitableSemicategory I

open import categories.DSM (SuitableSemicategory.wildsemicatstr I)

{- Shapes -}

ℕ³ = ℕ × ℕ × ℕ

is-shape : (i h t : ℕ) → Type₀
is-shape i h t = (h ≤ i) × (t ≤ hom-size i h)

hcond = fst
tcond = snd

is-shape-Σ : ℕ³ → Type₀
is-shape-Σ (i , h , t) = is-shape i h t

abstract
  is-shape-is-prop : ∀ {i h t} → is-prop (is-shape i h t)
  is-shape-is-prop =
    all-paths-is-prop
      λ{(hcond , tcond)
        (hcond' , tcond')
      → is-shape= (prop-path ≤-is-prop hcond hcond')
                  (prop-path ≤-is-prop tcond tcond')}
    where
    is-shape= : ∀ {i h t} {iS iS' : is-shape i h t}
      → hcond iS == hcond iS' → tcond iS == tcond iS' → iS == iS'
    is-shape= idp idp = idp

is-shape? : ∀ i h t → Dec (is-shape i h t)
is-shape? i h t = ×-dec (h ≤? i) (t ≤? hom-size i h)

is-shape-Σ? : ∀ s → Dec (is-shape-Σ s)
is-shape-Σ? (i , h , t) = is-shape? i h t

shapeₕ↓ : ∀ {i h} → is-shape i (1+ h) O → is-shape i h (hom-size i h)
shapeₕ↓ iS = ≤-trans lteS (hcond iS) , lteE

shapeₜ↓ : ∀ {i h t} → is-shape i h (1+ t) → is-shape i h t
shapeₜ↓ iS = hcond iS , ≤-trans lteS (tcond iS)

empty-shape : ∀ i → is-shape i O O
empty-shape i = O≤ i , O≤ (hom-size i O)

full-shape-1+ : ∀ i → is-shape (1+ i) i (hom-size (1+ i) i)
full-shape-1+ i = lteS , lteE

-- Shape order (on shapes with equal apexes)
_≤ₛ_ : (s s' : ℕ × ℕ) → Type₀
(h , t) ≤ₛ (h' , t') = (h < h') ⊔ ((h == h') × (t ≤ t'))

OO≤ₛ : ∀ h t → (O , O) ≤ₛ (h , t)
OO≤ₛ O t = inr (idp , O≤ t)
OO≤ₛ (1+ h) t = inl (O<S h)

-- Shape equivalence: generated by (i, h, t) ~ (i, h + 1, 0)

infix 90 _~[_]_ _~⋆_[_]_

_~[_]_ : ((h₁ , t₁) : ℕ × ℕ) (i : ℕ) ((h₂ , t₂) : ℕ × ℕ) → Type₀
(h₁ , t₁) ~[ i ] (h₂ , t₂) = (t₁ == hom-size i h₁) × (h₂ == 1+ h₁) × (t₂ == O)

_~⋆_[_]_ :
  ((h₁ , t₁) : ℕ × ℕ) (n : ℕ) (i : ℕ) ((h₂ , t₂) : ℕ × ℕ) → Type₀
(h₁ , t₁) ~⋆ O [ i ] (h₂ , t₂) = (h₁ , t₁) == (h₂ , t₂)
(h₁ , t₁) ~⋆ 1+ n [ i ] (h₂ , t₂) =
  Σ[ h ː ℕ ] Σ[ t ː ℕ ] Σ[ iS ː is-shape i h t ]
    (h₁ , t₁) ~[ i ] (h , t) × (h , t) ~⋆ n [ i ] (h₂ , t₂)

-- Shape restriction

-- [ i , h , t ] iS · f is a representative of the equivalence class of shapes
-- that describe the (i, h, t)-sieve restricted along f (no uniform choice of which).
-- The definition is a bit finicky, and it's currently a bit unclear what the best
-- formulation is.

module _ (i : ℕ) where
  shape-· : (h t : ℕ) (iS : True (is-shape? i h t)) {m : ℕ} (f : hom i m) → ℕ × ℕ
  shape-· h (1+ t) iS {m} f =
    if h <? m then (λ h<m →
      if O <? hom-size m h then (λ O<hom-size
        → h , #-factors-of-hom[ i , h ]≤[ t-Fin ]-through f O<hom-size)
      else λ _
        → h , O)
    else λ _
      → m , O
    where
      t-Fin = t , <-≤-< ltS (tcond ⌜ iS ⌝)
  shape-· (1+ h) O iS {m} f = shape-· h (hom-size i h) (by $ shapeₕ↓ ⌜ iS ⌝) f
  shape-· O O _ {m} f = O , O

  abstract
    height-shape-· : ∀ h t iS {m} (f : hom i m) → fst (shape-· h t iS f) ≤ m
    height-shape-· h (1+ t) iS {m} f with h <? m | O <? hom-size m h
    ... | inl u | inl _ = inr u
    ... | inl u | inr _ = inr u
    ... | inr _ | _ = lteE
    height-shape-· (1+ h) O iS f = height-shape-· h (hom-size i h) (by $ shapeₕ↓ ⌜ iS ⌝) f
    height-shape-· O O iS f = O≤ _

    height-shape-·' : ∀ h t iS {m} (f : hom i m) → fst (shape-· h t iS f) ≤ h
    height-shape-·' h (1+ t) iS {m} f with h <? m | O <? hom-size m h
    ... | inl _    | inl _ = lteE
    ... | inl _    | inr _ = lteE
    ... | inr ¬h<m | _
          with ℕ-trichotomy' m h
    ...   | inl u = u
    ...   | inr u = ⊥-rec $ ¬h<m u
    height-shape-·' (1+ h) O iS f =
      ≤-trans (height-shape-·' h (hom-size i h) (by $ shapeₕ↓ ⌜ iS ⌝) f) lteS
    height-shape-·' O O iS f = lteE

    width-shape-· :
      ∀ h t iS {m} (f : hom i m)
      → snd (shape-· h t iS f) ≤ hom-size m (fst (shape-· h t iS f))
    width-shape-· h (1+ t) iS {m} f with h <? m | O <? hom-size m h
    ... | inl u | inl v = #-factors-ub t-Fin f v
                          where t-Fin = t , <-≤-< ltS (tcond ⌜ iS ⌝)
    ... | inl _ | inr _ = O≤ _
    ... | inr x | s = O≤ _
    width-shape-· (1+ h) O iS f = width-shape-· h (hom-size i h) (by $ shapeₕ↓ ⌜ iS ⌝) f
    width-shape-· O O iS f = O≤ _


[_,_,_]_· : (i h t : ℕ) (iS : True (is-shape? i h t)) {m : ℕ} (f : hom i m) → ℕ³
[_,_,_]_· i h t iS {m} f = m , shape-· i h t iS f

abstract
  ·-is-shape : ∀ i h t iS {m} (f : hom i m) → is-shape-Σ ([ i , h , t ] iS · f)
  ·-is-shape i h t iS {m} f =
    height-shape-· i h t iS f , width-shape-· i h t iS f


{- (i, h, t)-admissibility -}

is-_-admissible : ((i , h , t) : ℕ³) {m : ℕ} (f : hom i m) → Type₀
is-(i , h , t)-admissible f =
  (cod f < h)
  ⊔ ((cod f == h) × (to-ℕ (idx-of f) < t))

is-_-admissible? :
  ((i , h , t) : ℕ³) {m : ℕ} (f : hom i m)
  → Dec (is-( i , h , t )-admissible f)
is- _ -admissible? f = ⊔-dec (_ <? _) (×-dec (_ ≟-ℕ _) (_ <? _))

admissibleₕ↑ : ∀ i h {m} (f : hom i m)
  → is-( i , h , hom-size i h )-admissible f
  → is-( i , 1+ h , O )-admissible f
admissibleₕ↑ i h f (inl u) = inl (ltSR u)
admissibleₕ↑ i h f (inr (idp , _)) = inl ltS

admissibleₕ↓ : ∀ i h {m} (f : hom i m)
  → is-( i , 1+ h , O )-admissible f
  → is-( i , h , hom-size i h )-admissible f
admissibleₕ↓ i h {m} f (inl u) with ℕ-trichotomy' m h
... | inr h<m = ⊥-rec (no-between u (<-ap-S h<m))
... | inl (inl idp) = inr (idp , idx<hom-size f)
... | inl (inr m<h) = inl m<h

admissible-h-iff : ∀ i h {m} (f : hom i m)
  → to-Bool (is-(i , h , hom-size i h)-admissible? f)
    == to-Bool (is-(i , 1+ h , O)-admissible? f)
admissible-h-iff i h f =
  ap-to-Bool
    (is-(i , h , hom-size i h)-admissible? f)
    (is-(i , 1+ h , O)-admissible? f)
    (admissibleₕ↑ i h f)
    (admissibleₕ↓ i h f)

-- Important

{- ·-admissible :
  (i h t : ℕ) (iS : is-shape i h t)
  {m : ℕ} (f : hom i m) (g : hom m h)
  → is-(i , h , t)-admissible (g ◦ f)
  → is- [ i , h , t ] iS · f -admissible g
·-admissible i O O iS f g (inl ())
·-admissible i O O iS f g (inr ())
·-admissible i (1+ h) O iS f g (inl u) = ⊥-rec $ ¬< u
·-admissible i .(1+ _) (1+ t) iS f g (inl (ltSR u)) = ⊥-rec $ S≮ u
·-admissible i h (1+ t) iS {m} f g (inr (p , u))
 with h <? m
... | inr ¬h<m = ⊥-rec $ ¬h<m (hom-inverse m h g)
... | inl  h<m
     with O <? hom-size m h
...     | inl v = inr (p , {!!})
{-
  g ◦ f ≼ [t] -- idx-of (g ◦ f) < 1+ t
  ⊢ idx-of (g: m → h) < cumul-#-factors-of ([t]: i → h) through (f: i → m) -- (from [0])

  Because:
    If idx-of g ≥ cumul-#-factors-of [t] through f,
    then idx-of (g ◦ f) ≥ 1+ t.
-}
...     | inr ¬v = ⊥-rec $ ¬v $ hom[ m , h ]-inhab g -}


{- Sieves -}

record LinearSieve (i : ℕ) : Type ℓₘ where
  constructor S[_,_]
  field
    height width : ℕ
    ⦃ shape-cond ⦄ : is-shape i height width
    char : DSM i
    char-∋-cond :
      ∀ {m} (f : hom i m)
      → (char ∋ f) == to-Bool (is-(i , height , width)-admissible? f)

open LinearSieve

linear-sieve : (i h t : ℕ) → is-shape i h t → LinearSieve i
linear-sieve i h t iS =
  S[ h , t ] ⦃ iS ⦄
    (λ _ f → to-Bool (is-(i , h , t )-admissible? f))
    (λ _ → idp)

{-
_~⋆⟨_⟩_ : ∀ {i} → LinearSieve i → (n : ℕ) → LinearSieve i → Type₀
_~⋆⟨_⟩_ {i} s n s' = (height s , width s) ~⋆⟨ n ⟩[ i ] (height s' , width s')

~⋆-equal-char : ∀ {n} {i} (s s' : LinearSieve i) → s ~⋆⟨ n ⟩ s' → char s == char s'
~⋆-equal-char {O} (S[ _ , _ ] χ χ-∋-cond) (S[ _ , _ ] χ' χ'-∋-cond) idp
  = DSM= (λ m f → χ-∋-cond f ∙ ! (χ'-∋-cond f))
~⋆-equal-char {1+ n} {i}
  s@(S[ h , .(hom-size i h) ] χ χ-∋-cond)
  s'@(S[ h' , t' ] χ' χ'-∋-cond)
  (.(1+ h) , .O , iS , (idp , idp , idp) , ~⋆) =
    DSM= (λ m f → χ-∋-cond f ∙ admissible-h-iff i h f)
    ∙ ~⋆-equal-char (linear-sieve i (1+ h) O iS) s' ~⋆
-}
